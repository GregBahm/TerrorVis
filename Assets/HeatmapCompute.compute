#pragma kernel HeatmapCompute
#pragma kernel ClearBuffers

#define TextureResolution 256

struct BufferPoint
{
    float2 Pos;
    int ForeignKills;
    int DomesticKills;
    float Time;
};
 
float _Time;
float _Range;

StructuredBuffer<BufferPoint> _DataBuffer;
RWStructuredBuffer<int> _ForeignKillsBuffer;
RWStructuredBuffer<int> _DomesticKilsBuffer;

uint UvsToIndex(float2 uv)
{
    uint yPart = (uint)(uv.y * TextureResolution) * TextureResolution;
    uint xPart = uv.x * TextureResolution; 
    return xPart + yPart;
}

[numthreads(128,1,1)]
void HeatmapCompute (uint3 id : SV_DispatchThreadID)
{
    BufferPoint dataPoint = _DataBuffer[id.x];
    uint index = UvsToIndex(dataPoint.Pos);

    float timeWeight = abs(dataPoint.Time - _Time);
    timeWeight = timeWeight / _Range;
    timeWeight = saturate(timeWeight);
    timeWeight = pow(timeWeight, 2);

    float fill = saturate(_Range - 1) * 2;
    timeWeight *=  1 - fill;
    timeWeight = 1 - timeWeight;

    InterlockedAdd(_ForeignKillsBuffer[index], dataPoint.ForeignKills * timeWeight);
    InterlockedAdd(_DomesticKilsBuffer[index], dataPoint.DomesticKills * timeWeight);
}

 
[numthreads(128,1,1)]
void ClearBuffers (uint3 id : SV_DispatchThreadID)
{
    _ForeignKillsBuffer[id.x] = 0;
    _DomesticKilsBuffer[id.x] = 0;
}